pkgname='libspud-git'
pkgver=r781.3852c8f
pkgrel=1
pkgdesc="A generic system for defining, writing and processing options files for scientific computer models"
arch=("x86_64")
url="http://www.imperial.ac.uk/earth-science/research/research-groups/amcg/software/spud/"
license=('LGPLv2.1')
depends=("python2" "pygtk" "python2-lxml" "trang" "libxml2")
makedepends=("make" "git" "gcc")
provides=("python2-dxdiff")
conflicts=("libspud-bzr")
source=(
"_$pkgname::git+https://github.com/FluidityProject/spud"
"$pkgname.patch"
)
noextract=("_$pkgname" "$pkgname.patch")

md5sums=('SKIP'
         '5f4597f7919ae0c7e7b058a620dbd79b')

prepare() {
    cd "$srcdir/_$pkgname"
    patch --strip=1 --input="$srcdir/$pkgname.patch"
}

pkgver() {
    cd "$srcdir/_$pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/_$pkgname"
    ./configure --prefix=/usr
    make
}

package() {
    cd "$srcdir/_$pkgname"
    make DESTDIR="$pkgdir/" install
    # Remove tinyxml.h since it conflicts with tinyxml
    # but is identical to the file provided by that package
    rm "$pkgdir/usr/include/tinyxml.h"
}
