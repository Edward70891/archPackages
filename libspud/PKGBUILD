pkgname='libspud-bzr'
pkgver=r539
pkgrel=1
pkgdesc="A generic system for defining, writing and processing options files for scientific computer models"
arch=("x86_64")
url="http://www.imperial.ac.uk/earth-science/research/research-groups/amcg/software/spud/"
license=('LGPLv2.1')
depends=("python2" "pygtk" "python2-lxml" "trang" "libxml2")
makedepends=("make" "bzr" "gcc")
provides=("python2-dxdiff")
source=(
"_$pkgname::bzr+https://code.launchpad.net/~spud/spud/trunk"
"$pkgname.patch"
)
noextract=("_$pkgname" "$pkgname.patch")

md5sums=('SKIP'
         '58e9bc403796ae370f48e45def3e0c63')

prepare() {
    cd "$srcdir/_$pkgname"
    patch --strip=1 --input="$srcdir/$pkgname.patch"
}

pkgver() {
    cd "$srcdir/_$pkgname"
    printf "r%s" "$(bzr revno)"
}

build() {
    cd "$srcdir/_$pkgname"
    ./configure --prefix=/usr
    make
}

package() {
    cd "$srcdir/_$pkgname"
    make DESTDIR="$pkgdir/" install
    # Remove tinyxml.h since it conflicts with tinyxml
    # but is identical to the file provided by that package
    rm "$pkgdir/usr/include/tinyxml.h"
}
